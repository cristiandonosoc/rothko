# Copyright 2019, Cristi√°n Donoso.
# This code has a BSD license. See LICENSE.

# MSVC ---------------------------------------------------------------------------------------------

if (compiler == "msvc") {
  config("default_warnings") {
    cflags = [
      "/W4",
      "/WX",

      "/w34062",  # W4062: All switch values must be handled.

      # Warning ignores
      "/wd4267",  # size_t to int.
      "/wd4244",  # int to boolean.
      "/wd4700",  # Un-initialized variable.
      "/wd4702",  # Un-reachable code.
      "/wd4996",  # fopen.
      "/wd4201",  # Anonymouse structs within union.
    ]
  }

  config("compiler") {
    defines = [
      "NOMINMAX",  # Remove windows stupid leaky defines.
      "_ITERATOR_DEBUG_LEVEL=0",  # Remove MSVC C++ iterator checking.
    ]

    cflags_cc = [
      "/std:c++latest",
      "/EHsc",  # Disable exceptions.
      "/GR-",  # Disable RTTI.
    ]

    ldflags = [
      "/SUBSYSTEM:CONSOLE",
      "/opt:ref",
      "user32.lib",
      "comctl32.lib",
      "Ole32.lib",
    ]
  }

  config("debug") {
    cflags = [
      "/Od",
      "/Zi",
    ]

    ldflags = [ "/DEBUG" ]
  }

  config("release") {
    cflags = [ "/O2" ]
  }
}

# clang --------------------------------------------------------------------------------------------

if (compiler == "clang") {
  config("default_warnings") {
    cflags = [
      "-Wall",
      "-Wextra",
      "-Werror",
      "-Wno-unused-function",
      "-Wmissing-prototypes",
    ]
  }

  config("compiler") {
    cflags_cc = [
      "-std=c++1z",
      "-fno-exceptions",
      "-fno-rtti",
    ]
  }

  config("debug") {
    cflags = [
      "-g",
      "-O0",
    ]
  }

  config("release") {
    cflags = [ "-O2" ]
  }
}

# macOS --------------------------------------------------------------------------------------------

# These are special frameworks we need to pass on to the compiler in order for things like clipboard
# to work. Otherwise we get weird linker "underfined symbol" errors.
config("osx_compiler_config") {
  ldflags = [
    "-framework",
    "Foundation",
    "-framework",
    "AppKit",
  ]
}
